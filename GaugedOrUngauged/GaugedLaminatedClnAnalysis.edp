load "UMFPACK64"
include "../LaminatedGeometry.idp"
include "../PlotInGMSH.idp"

int coil=Th( (PosCoilIn+PosCoilOut)/2,PosCoilIn/4).region;
int lamin1=Th( PosCoilIn/2,1*LaminDepth/2).region;  
int lamin2=Th( PosCoilIn/2,3*LaminDepth/2).region;  
int lamin3=Th( PosCoilIn/2,5*LaminDepth/2).region;  
int lamin4=Th( PosCoilIn/2,7*LaminDepth/2).region;  
int lamin5=Th( PosCoilIn/2,9*LaminDepth/2).region;  
int airIn=Th( PosCoilIn-RadiusCoil/2,PosCoilIn-RadiusCoil/2).region;
int airOut=Th( 1.1*PosCoilOut,1.1*PosCoilOut).region;

int numStages = 5;
real [int] lambda(2*numStages+1);
real RR;
real nu0=1e7/4/pi;
real nu1=1e4/4/pi;

func co = 1.0*(region==coil);
func yo = 1.0*(region==lamin1) +1.0*(region==lamin2) +1.0*(region==lamin3)
         +1.0*(region==lamin4) +1.0*(region==lamin5);
func ar = 1.0*(region==airIn)+1.0*(region==airOut)+1.0*(region==coil);
func sig=1e6*yo+4e7*co;
func nu=nu0*ar + nu1*yo;

//plot(Th);

// --------------------------------
// Solving laplacian(phi)=0
// --------------------------------
fespace Ph(Th,P1);
Ph u,v;
macro Grad(u) [dx(u), dy(u)] //

solve Poisson(u,v, solver=CG)
	=int2d(Th) ( 4e7*co * Grad(u)' * Grad(v))
	+on(CoilTerminalIn, u=1)
	+on(CoilTerminalOut, u=0);

fespace Eh(Th,RT0Ortho);
Eh [Esx, Esy];
Eh [E0x,E0y] = Grad(u);

// ------------------------------
// defining magnetostatic problem
// curl(A')*nu*Curl(A)=R*Crul(A')*T
// ------------------------------
fespace Xh(Th,[RT0Ortho,P1]);
Xh [Ax,Ay,p], [ax,ay,q];
varf mags([Ax,Ay,p], [ax,ay,q])
	= int2d(Th)( nu * (dx(ay)-dy(ax)) * (dx(Ay)-dy(Ax)) 
                + Grad(p)' * [ax,ay] + Grad(q)' * [Ax,Ay] )
	 -int2d(Th)( RR * sig * [ax,ay]' * [Esx,Esy] )
	 +on(CoilTerminalIn,CoilTerminalOut, Ax=0,Ay=0,p=0)
	 +on(AirInBot1,AirOutBot,AirOutLeft, Ax=0,Ay=0,p=0)
	 +on(Lamin1Left,Lamin2Left,Lamin3Left,Lamin4Left,Lamin5Left, Ax=0,Ay=0,p=0)
	 +on(AirInLeft1,AirInLeft2,AirInLeft3,AirInLeft4,AirInLeft5,AirInLeft6, Ax=0,Ay=0,p=0)
	 ;
matrix<real> Kmags = mags(Xh,Xh,tgv=1e30,solver=UMFPACK);

// --------------------------------
// defining electrokinetic equation
// div( sig * (A+Grad(phi)) ) = 0
// --------------------------------
Ph pp;
varf elek(pp, qq)
		=int2d(Th)( sig * Grad(qq)' * Grad(pp))
		-int2d(Th)( sig * Grad(qq)' * [Esx,Esy] )
		+on(Lamin1Left,Lamin2Left,Lamin3Left,Lamin4Left,Lamin5Left,pp=0)
		+on(CoilTerminalIn,CoilTerminalOut,pp=0)
		;
matrix<real> Kelek = elek(Ph,Ph,solver=CG,eps=1e-20);

system("del LaminatedLamda.txt");
ofstream gg("LaminatedLamda.txt",append);

Eh[int] [Enx,Eny](numStages+1);
Eh[int] [Anx,Any](numStages);
[Enx[0],Eny[0]] = Grad(u);
for (int stage=0; stage<numStages; stage++)
{
	[Esx,Esy] = [Enx[stage],Eny[stage]];
	lambda[2*stage] = 1./int2d(Th)(sig  * [Esx, Esy]' * [Esx, Esy]);
	gg.scientific << lambda[2*stage] << endl;
	cout <<"R"<< 2*stage <<" = "<< lambda[2*stage] << endl;
	RR = lambda[2*stage];
	real[int] Fmags = mags(0,Xh);
	real[int] ansMags = Kmags^-1*Fmags;
	Ax[] = -ansMags;
	if (stage==0){
		[Anx[0],Any[0]] = [Ax,Ay];
	}
	else{
		[Anx[stage],Any[stage]] = [Anx[stage-1]+Ax,Any[stage-1]+Ay];
	}
	lambda[2*stage+1] = int2d(Th)(nu * (dx(Any[stage])-dy(Anx[stage])) * (dx(Any[stage])-dy(Anx[stage])));
	gg.scientific << lambda[2*stage+1] << endl;
	cout <<"L" << 2*stage+1 <<" = "<< lambda[2*stage+1] << endl;
	[Esx,Esy] = [Anx[stage]/lambda[2*stage+1],Any[stage]/lambda[2*stage+1]];
	real[int] Felek = elek(0,Ph);
	real[int] ansElek = Kelek^-1*Felek;
	pp[] = -ansElek;
	[Enx[stage+1], Eny[stage+1]] = [Enx[stage], Eny[stage]] - [Anx[stage]/lambda[2*stage+1], Any[stage]/lambda[2*stage+1]] + Grad(pp);
}


// -------------------------------------
// --------PLOTTING THE RESULTS---------
// -------------------------------------
system("del graph.pos");

for (int stage=0; stage<numStages; stage++)
{
	Eh [J0x,J0y] = sig*[Enx[stage], Eny[stage]];
	gmshVectorPlot("J"+2*stage,Th,J0x,J0y);
	gmshVectorPlot("A"+(2*stage+1),Th,Anx[stage],Any[stage]);
}
